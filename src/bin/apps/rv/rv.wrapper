#!/bin/tcsh -f

if (-r $HOME/.config/wt_rvglsyncsetup) source $HOME/.config/wt_rvglsyncsetup

setenv RV_STARTED `date +%s.%N`

setenv RV_IS_OPENRV 1

if ($?RV_SUPPORT_PATH) then
    setenv RV_SUPPORT_PATH /usr/home/$USER/.rv:$RV_SUPPORT_PATH
else
    setenv RV_SUPPORT_PATH /usr/home/$USER/.rv
endif

# we may need to vet / whitelist the PYTHONPATH for runtime load speed
if ($?RV_WETA_ENV) then
    if (-e $RV_WETA_ENV && -r $RV_WETA_ENV && -x $RV_WETA_ENV) then
        setenv ENV_PYTHON_PATH $PYTHONPATH
        setenv PYTHONPATH `$RV_WETA_ENV`
    endif
endif

# TODO: allow codecThreads to be set from the command-line?
if (! $?RV_MOVIEFFMPEG_ARGS) then
    setenv RV_MOVIEFFMPEG_ARGS "--codecThreads 4"
endif


#   This script sets the RV_HOME environment (if not already set),
#   makes sure the plugins are the path and launches the actual binary.
#
#   NOTE: doesn't properly set RV_HOME if invoked from a link to the script
#   and RV_HOME is not already set
#

set noglob

#
#   Workaround for pulseaudio/libsndfile package mismatch on FC/Cent that causes
#   crash when accessing preferences.
#
if ($?PULSE_RUNTIME_PATH) then
    if ((-e /usr/lib64/libpulsedsp.so || -e /usr/lib/libpulsedsp.so || -e /usr/lib/x86_64-linux-gnu/libpulsedsp.so) && ! $?RV_DISABLE_PULSEAUDIO_WORKAROUND) then
        if ($?LD_PRELOAD) then
            setenv LD_PRELOAD ${LD_PRELOAD}:libpulsedsp.so
        else
            setenv LD_PRELOAD libpulsedsp.so
        endif
    endif
    # UR-266 libpulsedsp.so location has moved on 16.04, detect and use it from new location so audio playback works on 16.04
    if ((-e /usr/lib/x86_64-linux-gnu/pulseaudio/libpulsedsp.so) && ! $?RV_DISABLE_PULSEAUDIO_WORKAROUND) then
        if ($?LD_PRELOAD) then
            setenv LD_PRELOAD ${LD_PRELOAD}:/usr/lib/x86_64-linux-gnu/pulseaudio/libpulsedsp.so
        else
            setenv LD_PRELOAD /usr/lib/x86_64-linux-gnu/pulseaudio/libpulsedsp.so
        endif
    endif
endif

if ($?QT_PLUGIN_PATH) then 
    # commented out as requested in https://jira.wetafx.co.nz/browse/DSERV-2321
    #echo "INFO: warning: QT_PLUGIN_PATH is set, which can cause RV to load the wrong Qt libraries/plugins.  Unsetting..."
    unsetenv QT_PLUGIN_PATH
endif

#
#   For unknown reasons, LANG causes problems when set to
#   interesting values (like fr_FR.UTF-8).
#
unsetenv LANG

if (! $?RV_HOME) then
    set canonicalName = "`readlink -f $0`"
    set binName = "$canonicalName:h"
    setenv RV_HOME "$binName:h"
endif

set rvbin = "$RV_HOME/bin/rv.bin"

unsetenv BUILD_ROOT
setenv PATH "$RV_HOME/bin:${PATH}"

if ($?LD_LIBRARY_PATH) then
     setenv LD_LIBRARY_PATH "$RV_HOME/lib:$LD_LIBRARY_PATH"
else
     setenv LD_LIBRARY_PATH "$RV_HOME/lib"
endif

setenv  RV_CLI_ARGS "$rvbin $*"

# if we are being called by a browser with an argument to a file in /tmp/${USER} or /local1/${USER}/Downloads open up permissions (DSERV-6982),  or if being called by pid 1 (as happens when the rv.desktop application launcher is used) which implies graphical interface usage.
setenv PPID `cat /proc/self/status | grep PPid | cut -f 2 -d ':'`
if ( `readlink /proc/${PPID}/exe | grep -E '/firefox$|/chrome$|/firefox-bin$' -c` != 0  || ${PPID} == 1 ) then
    if ( `echo ${1} | grep -E "/tmp/${USER}/.*\.mov|/local1/${USER}/Downloads/.*\.mov" -c` != 0 ) then
        chmod a+r ${1}
        chmod ug+w ${1}
    endif
endif

# ensure pyjet is first on PYTHONPATH, and get us the best reduction in filesystem operations
# https://wetafx.atlassian.net/jira/servicedesk/projects/TECH/queues/custom/109/TECH-2876
if ($?PYJET_PYTHONPATH) then
  setenv PYTHONPATH ${PYJET_PYTHONPATH}:${PYTHONPATH}
endif

# Add support for phoenix: RV-140
if ($?WTAPP_PHOENIX_ROOT) then
    if ($?RV_SUPPORT_PATH) then
        setenv RV_SUPPORT_PATH ${WTAPP_PHOENIX_ROOT}/py/RV_SUPPORT_PATH:$RV_SUPPORT_PATH
    else
        setenv RV_SUPPORT_PATH ${WTAPP_PHOENIX_ROOT}/py/RV_SUPPORT_PATH
    endif
endif

if ( ! -d "/local1/${USER}" ) then
    mkdir -p "/local1/${USER}"
endif

exec $rvbin -prefsPath /local1/${USER} $*:q
